/* Licensed to the Apache Software Foundation (ASF) under one
   or more contributor license agreements.  See the NOTICE file
   distributed with this work for additional information
   regarding copyright ownership.  The ASF licenses this file
   to you under the Apache License, Version 2.0 (the
   "License"); you may not use this file except in compliance
   with the License.  You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing,
   software distributed under the License is distributed on an
   "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
   KIND, either express or implied.  See the License for the
   specific language governing permissions and limitations
   under the License.
*/

ext {
    apply from: 'cordova.gradle'
    cdvCompileSdkVersion = privateHelpers.getProjectTarget()
    cdvBuildToolsVersion = privateHelpers.findLatestInstalledBuildTools()
}

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://maven.google.com' }
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://groovy.jfrog.io/artifactory/libs-release/' }
        maven { url 'https://nexus.web.cern.ch/nexus/content/repositories/public/' }
    }

    dependencies {
        // The gradle plugin and the maven plugin have to be updated after each version of Android
        // studio comes out
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = 'org.apache.cordova'
version = '8.1.0'

android {
    compileSdkVersion cdvCompileSdkVersion
    buildToolsVersion cdvBuildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // For the Android Cordova Lib, we will hardcode the minSdkVersion and not allow changes.
    defaultConfig {
        minSdkVersion 24
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
    }
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            groupId = 'org.apache.cordova'
            artifactId = 'cordova-android'
            version = '8.1.0'

            // Define the AAR artifact
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")

            // Configure the POM, similar to what you had in install {}
            pom {
                name = 'Cordova'
                description = 'Apache Cordova Android Library'
                url = 'https://cordova.apache.org'
                packaging = 'aar' // Set packaging explicitly

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // Add the rest of your developer/scm information here
                developers {
                    developer {
                        id = 'stevengill'
                        name = 'Steve Gill'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/apache/cordova-android.git'
                    developerConnection = 'scm:git:git@github.com:apache/cordova-android.git'
                    url = 'https://github.com/apache/cordova-android'
                }
            }
        }
    }
}

tasks.register('sourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'cordova-android'
        userOrg = 'cordova'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/apache/cordova-android'
        websiteUrl = 'https://cordova.apache.org'
        issueTrackerUrl = 'https://github.com/apache/cordova-android/issues'
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        labels = ['android', 'cordova', 'phonegap']
        version {
            name = '8.1.0'
            released  = new Date()
            vcsTag = '8.1.0'
        }
    }
}
